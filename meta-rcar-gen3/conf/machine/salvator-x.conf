#@TYPE: Machine
#@NAME: Salvator-X machine
#@DESCRIPTION: Machine configuration for running Salvator-X

require conf/machine/include/${SOC_FAMILY}.inc
# Add default tune for H3/M3
DEFAULTTUNE ?= "cortexa57-cortexa53"
# Add default tune for M3N
DEFAULTTUNE_r8a77965 = "cortexa57"

# Add tune configure file for M3N
TUNE_CONF ?= "conf/machine/include/tune-cortexa57-cortexa53.inc"
TUNE_CONF_r8a77965 = "conf/machine/include/tune-cortexa57.inc"

require ${TUNE_CONF}

# 32BIT package install (default is disable)
# This variables can be used only in multilib.
USE_32BIT_PKGS ?= "0"
USE_32BIT_WAYLAND ?= "0"
USE_32BIT_MMP ?= "0"

MACHINE_FEATURES = ""

KERNEL_IMAGETYPE = "Image"
IMAGE_FSTYPES_append = " tar.bz2 ext4 cpio.gz"

SERIAL_CONSOLE = "115200 ttySC0"

# Configuration for kernel
PREFERRED_PROVIDER_virtual/kernel = "linux-renesas"
# Device tree for H3
KERNEL_DEVICETREE_r8a7795 = " \
    renesas/r8a7795-es1-salvator-x.dtb \
    renesas/r8a7795-salvator-x.dtb \
    renesas/r8a7795-salvator-xs.dtb \
"
# Device tree for M3
KERNEL_DEVICETREE_r8a7796 = " \
    renesas/r8a7796-salvator-x.dtb \
    renesas/r8a7796-salvator-xs.dtb \
"
# Device tree for M3N
KERNEL_DEVICETREE_r8a77965 = " \
    renesas/r8a77965-salvator-x.dtb \
    renesas/r8a77965-salvator-xs.dtb \
"

# Configuration for ARM Trusted Firmware
EXTRA_IMAGEDEPENDS += " arm-trusted-firmware optee-os"

# u-boot
PREFERRED_VERSION_u-boot = "v2015.04%"
EXTRA_IMAGEDEPENDS += " u-boot"
# H3 u-boot configure
UBOOT_MACHINE_r8a7795 = "r8a7795_salvator-x_defconfig"
# M3 u-boot configure
UBOOT_MACHINE_r8a7796 = "r8a7796_salvator-x_defconfig"
# M3N u-boot configure
UBOOT_MACHINE_r8a77965 = "r8a77965_salvator-x_defconfig"

# libdrm
PREFERRED_VERSION_libdrm = "2.4.68"

# Add variable to Build Configuration in build log
BUILDCFG_VARS_append = " SOC_FAMILY"
